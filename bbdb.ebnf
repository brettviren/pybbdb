;; EBNF syntax definition for BBDB (version 2).

bbdb            = {bbdb_entry}* .
bbdb_entry      = "[" name aka company phone address net notes cache "]" .

name            = name_first name_last .
name_first      = string .
name_last       = string .

aka             = "(" {string}+ ")" | "nil" .

company         = string | "nil" .

phone           = "(" {phone_entry}+ ")" | "nil" .
phone_entry     = "[" phone_tag phone_number "]" .
phone_tag	= string .
phone_number    = phone_usa | phone_nonusa .
phone_usa       = {integer}+ .
phone_nonusa    = string .

address         = "(" {address_entry}+ ")" | "nil" .
address_entry   = "[" address_tag address_list city state zipcode country "]" .
address_tag     = string .
address_list    = "(" {string}+ ")" .
city            = string .
state           = string .
zipcode         = string .
country         = string .

net             = "(" {string}+ ")" | "nil" .

notes           = "(" {note}+ ")" | "nil" .
note            = "(" atom "." string ")" .

cache           = "nil" .

integer		= "[0-9]+" .
atom		= "[a-z][a-z-]*" .
